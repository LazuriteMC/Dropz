plugins {
	id 'architectury-plugin' version "$architectury_plugin_version"
	id 'dev.architectury.loom' version "$loom_version" apply false
	id 'java'
}

architectury {
	minecraft = rootProject.minecraft_version
}

subprojects {
	apply plugin: 'dev.architectury.loom'
	apply plugin: 'architectury-plugin'
	apply plugin: 'maven-publish'

	version dropz_version
	group maven_group

	archivesBaseName = rootProject.archivesBaseName + '-' + name
	loom.silentMojangMappingsLicense()

	repositories {
		maven { url 'https://lazurite.dev/releases' }
		maven { url 'https://maven.modmuss50.me' }
	}

	dependencies {
		minecraft "com.mojang:minecraft:$minecraft_version"
		mappings loom.officialMojangMappings()
	}

	java {
		toolchain {
			languageVersion = JavaLanguageVersion.of(17)
		}

		withSourcesJar()
	}


	publishing {
		repositories {
			maven {
				name 'Lazurite'
				url 'https://lazurite.dev/releases'

				credentials {
					username property('publish.lazurite.alias')
					password property('publish.lazurite.secret')
				}
			}
		}

		publications {
			"$name"(MavenPublication) {
				artifactId = archivesBaseName
				from components.java
			}
		}
	}
}