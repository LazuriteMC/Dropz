plugins {
	id 'fabric-loom' version "$loom_version"
	id 'maven-publish'
}

version dropz_version

repositories {
	maven { url 'https://lazurite.dev/releases' }
	maven { url 'https://maven.modmuss50.me' }
}

dependencies {
	minecraft "com.mojang:minecraft:$minecraft_version"
	mappings loom.officialMojangMappings()

	// Fabric
	modImplementation "net.fabricmc:fabric-loader:$fabric_loader_version"

	// Rayon
	modImplementation "dev.lazurite:rayon-fabric:$rayon_version"

	// Fiber
	modImplementation "me.zeroeightsix:fiber:$fiber_version"
	include "me.zeroeightsix:fiber:$fiber_version"
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}

	withSourcesJar()
}

tasks.withType ProcessResources, {
	inputs.properties(
			'version': project.version,
			'min_minecraft_version': min_minecraft_version,
			'min_fabric_loader_version': min_fabric_loader_version,
			'min_rayon_version': min_rayon_version
	)

	filesMatching 'fabric.mod.json', {
		expand(
				'version': project.version,
				'min_minecraft_version': min_minecraft_version,
				'min_fabric_loader_version': min_fabric_loader_version,
				'min_rayon_version': min_rayon_version
		)
	}
}

publishing {
	repositories {
		maven {
			name 'Lazurite'
			url 'https://lazurite.dev/releases'

			credentials {
				username property('publish.lazurite.alias')
				password property('publish.lazurite.secret')
			}
		}
	}

	publications {
		dropz MavenPublication, {
			from components.java
		}
	}
}